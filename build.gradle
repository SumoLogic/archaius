plugins {
    id "com.diffplug.spotless" version "8.1.0"
}

repositories {
    mavenCentral()
}

spotless {
    java {
        target 'archaius-*/src/**/*.java'
        eclipse().configFile('eclipse-formatter.xml')
    }
}

// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = 17
    targetCompatibility = 17

    repositories {
        mavenCentral()
    }
    dependencies {
        implementation 'com.google.code.findbugs:jsr305:3.0.1'
        testImplementation('log4j:log4j:1.2.16')
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    group = "com.netflix.${githubProjectName}"

//    tasks.withType(JavaCompile) {
//        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
//    }

    //JavaDoc linting is enabled by default and will fail
    tasks.withType(Javadoc).all {
      options.addBooleanOption('Xdoclint:none', true)
    }

    tasks.withType(Test) {
        forkEvery = 1
        jvmArgs '--add-opens=java.base/java.lang=ALL-UNNAMED',
                '--add-opens=java.base/java.util=ALL-UNNAMED'
    }
    test {
        maxHeapSize = '2g' // or however much memory the tests need
        testLogging {
            events 'failed', 'passed', 'skipped'
            showStandardStreams = false
        }
    }

    publishing {
        repositories {
            maven {
                url = "${artifactory_contextUrl}/${artifactory_repoName}"
                credentials {
                    username = "${artifactory_user}"
                    password = "${artifactory_password}"
                }
            }
        }

        publications {
            maven(MavenPublication) {
                groupId = 'com.netflix.archaius'
                version = '0.7.6.SUMO-7'

                artifact jar
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':archaius-core') {
    dependencies {
        api 'commons-configuration:commons-configuration:1.10'
        api 'org.slf4j:slf4j-api:1.6.4'
        api 'com.google.guava:guava:16.0'
        api 'com.fasterxml.jackson.core:jackson-annotations:2.4.3'
        api 'com.fasterxml.jackson.core:jackson-core:2.4.3'
        api 'com.fasterxml.jackson.core:jackson-databind:2.4.3'
        testImplementation 'junit:junit:4.11'
        testImplementation 'org.slf4j:slf4j-simple:1.7.5'
        testImplementation 'org.apache.derby:derby:10.8.2.2'
        testImplementation 'org.apache.commons:commons-io:1.3.2'
        testImplementation files('src/test/resources/classpathTestResources.jar')

    }
}

project(':archaius-aws') {
    dependencies {
        implementation project(':archaius-core')
        implementation("software.amazon.awssdk:dynamodb:2.29.17")
        implementation("software.amazon.awssdk:s3:2.29.17")
        testImplementation 'junit:junit:4.11'
        testImplementation 'org.mockito:mockito-all:1.9.5'
        testImplementation 'org.slf4j:slf4j-simple:1.6.4'
    }
}

project(':archaius-typesafe') {
    dependencies {
        implementation project(':archaius-core')
        implementation 'com.typesafe:config:1.2.1'
        testImplementation 'junit:junit:4.11'
        testImplementation 'org.slf4j:slf4j-simple:1.6.4'
    }
}

project(':archaius-etcd') {
    sourceCompatibility = 17
    targetCompatibility = 17

    dependencies {
        implementation project(':archaius-core')
        implementation 'com.google.guava:guava:19.0'
        implementation 'io.fastjson:etcd-client:0.33'
        testImplementation 'junit:junit:4.11'
        testImplementation 'org.mockito:mockito-all:1.9.5'
        testImplementation 'org.slf4j:slf4j-simple:1.6.4'
    }
}