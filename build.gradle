//plugins {
//    id 'nebula.netflixoss' version '7.1.3'
//    id 'nebula.provided-base' version '5.0.3'
//}

// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

subprojects {
//    apply plugin: 'nebula.netflixoss'
//    apply plugin: 'nebula.provided-base'
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }
    dependencies {
        compile 'com.google.code.findbugs:jsr305:3.0.1'
        testCompile('log4j:log4j:1.2.16')
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    group = "com.netflix.${githubProjectName}"

    //Java 8 has JavaDoc linting enabled by default and will fail
    tasks.withType(Javadoc).all {
      options.addBooleanOption('Xdoclint:none', true)
    }

    tasks.withType(Test) { forkEvery = 1 }
    test {
        maxHeapSize = '2g' // or however much memory the tests need
        testLogging {
            events 'started', 'failed', 'passed', 'skipped'
            showStandardStreams  = true
        }
    }

    publishing {
        repositories {
            maven {
                url = "${artifactory_contextUrl}/${artifactory_repoName}"
                credentials {
                    username = "${artifactory_user}"
                    password = "${artifactory_password}"
                }
            }
        }

        publications {
            maven(MavenPublication) {
                groupId = 'com.netflix.archaius'
                version = '0.7.6.SUMO-6'

                artifact jar
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':archaius-core') {
    dependencies {
        compile 'commons-configuration:commons-configuration:1.8'
        compile 'org.slf4j:slf4j-api:1.6.4'
        compile 'com.google.guava:guava:16.0'
        compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.3'
        compile 'com.fasterxml.jackson.core:jackson-core:2.4.3'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.4.3'
        testCompile 'junit:junit:4.11'
        testCompile 'org.slf4j:slf4j-simple:1.7.5'
        testCompile 'org.apache.derby:derby:10.8.2.2'
        testCompile 'org.apache.commons:commons-io:1.3.2'
        testCompile files('src/test/resources/classpathTestResources.jar')

    }
}

project(':archaius-aws') {
    dependencies {
        compile project(':archaius-core')
        compile("software.amazon.awssdk:dynamodb:2.29.17")
        compile("software.amazon.awssdk:s3:2.29.17")
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
    }
}

//project(':archaius-jclouds') {
//    dependencies {
//        compile project(':archaius-core')
//        compile 'org.jclouds:jclouds-blobstore:1.6.0'
//        testCompile 'junit:junit:4.11'
//        testCompile 'org.slf4j:slf4j-simple:1.6.4'
//    }
//}

project(':archaius-typesafe') {
    dependencies {
        compile project(':archaius-core')
        compile 'com.typesafe:config:1.2.1'
        testCompile 'junit:junit:4.11'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
    }
}

project(':archaius-zookeeper') {
    dependencies {
        compile project(':archaius-core')
        compile('org.apache.zookeeper:zookeeper:3.4.5') {
            exclude group: 'com.sun.jdmk', module: 'jmxtools'
            exclude group: 'com.sun.jmx', module: 'jmxri'
            exclude group: 'javax.jms', module: 'jms'
        }
        compile 'org.apache.curator:curator-client:2.3.0'
        compile 'org.apache.curator:curator-recipes:2.3.0'
        testCompile 'junit:junit:4.11'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
        testCompile 'org.apache.curator:curator-test:2.3.0'
    }
}

project(':archaius-etcd') {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile project(':archaius-core')
        compile 'com.google.guava:guava:19.0'
        compile 'io.fastjson:etcd-client:0.33'
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'org.slf4j:slf4j-simple:1.6.4'
    }
}

project(':archaius-scala') {
    apply plugin: 'scala'

    dependencies {
        compile project(':archaius-core')
        compile 'org.scala-lang:scala-library:2.10.4'
        testCompile 'org.scalatest:scalatest_2.10:3.0.8'
        testCompile 'junit:junit:4.11'
    }
}

project(':archaius-scala_2.11') {
    apply plugin: 'scala'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    buildDir = 'build_2.11'

    dependencies {
        compile project(':archaius-core')
        compile 'org.scala-lang:scala-library:2.11.11'
        testCompile 'org.scalatest:scalatest_2.11:3.0.8'
        testCompile 'junit:junit:4.11'
    }
}

project(':archaius-scala_2.12') {
    apply plugin: 'scala'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    buildDir = 'build_2.12'

    dependencies {
        compile project(':archaius-core')
        compile 'org.scala-lang:scala-library:2.12.11'
        testCompile 'org.scalatest:scalatest_2.12:3.0.8'
        testCompile 'junit:junit:4.11'
    }
}

project(':archaius-scala_2.13') {
    apply plugin: 'scala'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    buildDir = 'build_2.13'

    dependencies {
        compile project(':archaius-core')
        compile 'org.scala-lang:scala-library:2.13.3'
        testCompile 'org.scalatest:scalatest_2.13:3.0.8'
        testCompile 'junit:junit:4.11'
    }
}


project(':archaius-samplelibrary') {
    apply plugin: 'scala'

    dependencies {
        compile project(':archaius-scala')

        compile 'org.slf4j:slf4j-api:1.6.4'
        compile 'org.scala-lang:scala-library:2.10.1'

        testCompile 'org.scalatest:scalatest_2.10.0:1.8'
        testCompile 'junit:junit:4.11'
    }
    jar {
        from('src/main/java') {
            include 'META-INF/conf/springbeans.xml'
            include 'META-INF/conf/config.properties'
        }
    }
}
